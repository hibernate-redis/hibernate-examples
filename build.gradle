
description = 'Hibernate Redis Examples'
archivesBaseName = "hibernate-redis-example"

ext {
    generatedSourcesDir = file("${buildDir}/generated-sources")
    generatedTestSourcesDir = file("${buildDir}/generated-test-sources")
    querydslVersion = '3.7.2'
}

configurations {
    querydslapt
}

dependencies {
    compile project(':hibernate-redis')

    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version:hibernateVersion
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.7'
    compile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.2'
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.1'
    compile group: 'joda-time', name: 'joda-time', version:'2.3'
    compile group: 'org.joda', name: 'joda-convert', version:'1.5'
    compile group: 'org.springframework', name: 'spring-context', version:springVersion
    compile group: 'org.springframework', name: 'spring-beans', version:springVersion
    compile group: 'org.springframework', name: 'spring-tx', version:springVersion
    compile group: 'org.springframework', name: 'spring-orm', version:springVersion
    compile group: 'org.springframework.data', name: 'spring-data-jpa', version:'1.6.0.RELEASE'
    compile group: 'com.zaxxer', name: 'HikariCP', version:'2.4.5'
    compile group: 'com.jolbox', name: 'bonecp', version:'0.8.0.RELEASE'
    compile group: 'com.mysema.querydsl', name: 'querydsl-jpa', version:querydslVersion

    querydslapt group: 'com.mysema.querydsl', name: 'querydsl-apt', version:querydslVersion

    testCompile group: 'org.springframework', name: 'spring-test', version:springVersion
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.easytesting', name: 'fest-assert', version:'1.4'
    testCompile group: 'com.h2database', name: 'h2', version:'1.3.175'
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = ['-proc:only',
                            '-processor',
                            'com.mysema.query.apt.jpa.JPAAnnotationProcessor']
    destinationDir = generatedSourcesDir
}

task generateTestQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types for tests') {
    source = sourceSets.main.java + sourceSets.test.java
    classpath = configurations.testCompile + configurations.querydslapt
    options.compilerArgs = ['-proc:only',
                            '-processor',
                            'com.mysema.query.apt.jpa.JPAAnnotationProcessor']
    destinationDir = generatedTestSourcesDir
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileTestJava {
    dependsOn generateTestQueryDSL
    source generateTestQueryDSL.destinationDir
}
